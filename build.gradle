apply plugin: 'java'

group 'com.concurrentperformance.ringingmaster'
version '1.0.0'

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

repositories {
    mavenCentral()
}

configurations {
    jaxb
}

dependencies {
    // General
    compile 'com.google.guava:guava:19.0'
    compile 'net.jcip:jcip-annotations:1.0'

    // Logging
    compile 'org.slf4j:slf4j-api:1.7.21'

    jaxb 'com.sun.xml.bind:jaxb-xjc:2.2.7-b41'
    jaxb 'com.sun.xml.bind:jaxb-impl:2.2.7-b41'
    jaxb 'javax.xml.bind:jaxb-api:2.2.7'

    // TEST: General
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.10.19'

    // TEST: Logging
    testRuntime 'org.apache.logging.log4j:log4j-core:2.5'
    testRuntime 'org.apache.logging.log4j:log4j-slf4j-impl:2.5'
}

jar {
    manifest {
        attributes("Implementation-Version": version)
    }
}


def generatedPersistModelSchemaDir = "${projectDir}/src/main/resources"
def generatedPersistModelClassesDir = "${buildDir}/classes/generated/jaxb"
def generatedPersistModelGeneratedDir = "${projectDir}/src/generated/java"

// Add generated code into source set.
sourceSets.main.java.srcDir generatedPersistModelGeneratedDir


task generateRingingMasterPersistJaxbJavaFiles (group: 'ringingmaster') {

    // Drive the UP-TO-DATE mechanism
    inputs.dir generatedPersistModelSchemaDir
    outputs.dir generatedPersistModelClassesDir

    doFirst() {
        project.ant {
            taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
                    classpath: configurations.jaxb.asPath
            mkdir(dir: generatedPersistModelGeneratedDir)
            mkdir(dir: generatedPersistModelClassesDir)

            xjc(destdir: generatedPersistModelGeneratedDir, package: 'com.concurrentperformance.ringingmaster.persist.generated.v1') {
                schema(dir: generatedPersistModelSchemaDir, includes: "**/*.xsd")
                arg(value: "-nv")
                produces(dir: generatedPersistModelGeneratedDir, includes: "**/*.java")
            }
        }
    }
}

// Ensure the generated files are in the compiled source, along with forcing the generation process
compileJava.source generateRingingMasterPersistJaxbJavaFiles.outputs.files, sourceSets.main.java

clean{
    delete generatedPersistModelGeneratedDir
}
