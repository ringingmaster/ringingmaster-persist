apply plugin: 'java'

group 'org.ringingmaster'
version '1.0.0'

compileJava {
    sourceCompatibility = 12
    targetCompatibility = 12
}

repositories {
    mavenCentral()
}

configurations {
    jaxb
}

dependencies {
    // General
    implementation 'com.google.guava:guava:27.1-jre'

    // Logging
    implementation 'org.slf4j:slf4j-api:1.7.25'

    jaxb 'org.glassfish.jaxb:jaxb-xjc:2.2.11'
    jaxb 'org.glassfish.jaxb:jaxb-runtime:2.2.11'
    jaxb 'javax.activation:activation:1.1.1'

    implementation 'javax.xml.bind:jaxb-api:2.1'
    implementation 'com.sun.xml.bind:jaxb-impl:2.1.2'

    // TEST: General
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.8.47'

    // TEST: Logging
    testRuntimeOnly 'org.apache.logging.log4j:log4j-core:2.8.2'
    testRuntimeOnly 'org.apache.logging.log4j:log4j-slf4j-impl:2.8.2'
}

jar {
    manifest {
        attributes("Implementation-Version": version)
    }
}


def generatedPersistModelSchemaDir = "${projectDir}/src/main/resources"
def generatedPersistModelClassesDir = "${buildDir}/classes/generated/jaxb"
def generatedPersistModelGeneratedDir = "${projectDir}/src/generated/java"

// Add generated code into source set.
sourceSets.main.java.srcDir generatedPersistModelGeneratedDir


task generateRingingMasterPersistJaxbJavaFiles (group: 'ringingmaster') {

    // Drive the UP-TO-DATE mechanism
    inputs.dir generatedPersistModelSchemaDir
    outputs.dir generatedPersistModelClassesDir

    doFirst() {
        project.ant {
            taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
                    classpath: configurations.jaxb.asPath
            mkdir(dir: generatedPersistModelGeneratedDir)
            mkdir(dir: generatedPersistModelClassesDir)

            xjc(destdir: generatedPersistModelGeneratedDir, package: 'org.ringingmaster.persist.generated.v1') {
                schema(dir: generatedPersistModelSchemaDir, includes: "**/*.xsd")
                arg(value: "-nv")
                produces(dir: generatedPersistModelGeneratedDir, includes: "**/*.java")
            }
        }
    }
}

// Ensure the generated files are in the compiled source, along with forcing the generation process
compileJava.source generateRingingMasterPersistJaxbJavaFiles.outputs.files, sourceSets.main.java

clean{
    delete generatedPersistModelGeneratedDir
}
