configurations {
    jaxb
}

dependencies {
    jaxb 'com.sun.xml.bind:jaxb-xjc:2.2.7-b41'
    jaxb 'com.sun.xml.bind:jaxb-impl:2.2.7-b41'
    jaxb 'javax.xml.bind:jaxb-api:2.2.7'
}

def generatedPersistModelSchemaDir = "${projectDir}/src/main/resources"
def generatedPersistModelClassesDir = "${buildDir}/classes/generated/jaxb"
def generatedPersistModelGeneratedDir = "${projectDir}/src/generated/java"

// Add generated code into source set.
sourceSets.main.java.srcDir generatedPersistModelGeneratedDir

// Ensure the generated files are in the compiled source, along with forcing the generation process
compileJava.source generateRingingMasterPersistJaxbJavaFiles.outputs.files, sourceSets.main.java

task generateRingingMasterPersistJaxbJavaFiles (group: 'ringingmaster') {

    // Drive the UP-TO-DATE mechanism
    inputs.dir generatedPersistModelSchemaDir
    outputs.dir generatedPersistModelClassesDir

    doFirst() {
        project.ant {
            taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
                    classpath: configurations.jaxb.asPath
            mkdir(dir: generatedPersistModelGeneratedDir)
            mkdir(dir: generatedPersistModelClassesDir)

            xjc(destdir: generatedPersistModelGeneratedDir, package: 'com.concurrentperformance.ringingmaster.persist.generated.v1') {
                schema(dir: generatedPersistModelSchemaDir, includes: "**/*.xsd")
                arg(value: "-nv")
                produces(dir: generatedPersistModelGeneratedDir, includes: "**/*.java")
            }
        }
    }
}

clean{
    delete generatedPersistModelGeneratedDir
}
